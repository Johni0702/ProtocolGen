group 'de.johni0702.mc.protocol'
version '0.1-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

sourceSets {
    test {
        java {
            srcDir 'src/test/gen'
        }
    }
    gen {
        java {
            compileClasspath += main.output
        }
    }
}

configurations {
    genCompile.extendsFrom compile
}

clean {
    delete "src/gen"
    delete "src/test/gen"
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
    maven {
        url 'http://repo.johni0702.de/content/repositories/spacehq/'
    }
}

dependencies {
    compile 'io.netty:netty-all:4.0.30.Final'
    compile 'org.spacehq:opennbt:1.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.powermock:powermock-module-junit4:1.6.2'
    testCompile 'org.powermock:powermock-api-mockito:1.6.2'
}

apply from: 'generator.gvy'

compileGenJava.dependsOn genPacketSources
compileTestJava.dependsOn genPacketTestSources

jar {
    dependsOn genClasses
    dependsOn configurations.runtime
    from sourceSets.gen.output
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
}
